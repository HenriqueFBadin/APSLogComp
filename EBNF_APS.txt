PROGRAM     = SETUP, { ( VARDECL | BLOCK | "\n" ) } ;

SETUP       = "music_base", "{", INSTRUMENT, "\n", TEMPO, { "\n" }, "}" ;

VARDEC     = "var", IDENTIFIER, TYPE, [ "=", EXPRESSION ], "\n" ;

BLOCK       = "{", "\n", { STATEMENT }, "}" ;

STATEMENT   = ( 
                ASSIGNMENT
              | PLAYNOTE
              | PLAYSEQUENCE
              | PAUSE
              | REPEAT
              | IF
              | VARDEC
              | Î»
              ), "\n" ;

ASSIGNMENT  = IDENTIFIER, "=", EXPRESSION ;

PLAYNOTE    = "play_note", ( NOTE | IDENTIFIER ) ;

PLAYSEQUENCE= "play_sequence", ( SEQUENCE | IDENTIFIER ) ;

PAUSE       = "pause_duration", ( NUMBER | IDENTIFIER ) ;

REPEAT      = "repeat", EXPRESSION, "times", [ "while", BEXPRESSION ], BLOCK ;

IF          = "if", BEXPRESSION, BLOCK, [ "else", BLOCK ] ;

NOTE        = "note", NOTE_NAME, "duration", NUMBER ;

INSTRUMENT  = "instrument", STRING ;

TEMPO       = "tempo_base", NUMBER ;

SEQUENCE    = "[", SEQITEM, { SEQSEP, SEQITEM }, "]" ;

SEQITEM     = ( NOTE | IDENTIFIER ), "pause_duration", ( NUMBER | IDENTIFIER ) ;

SEQSEP      = "," ;

BEXPRESSION = BTERM, { "||", BTERM } ;

BTERM       = RELEXPRESSION, { "&&", RELEXPRESSION } ;

RELEXPRESSION = EXPRESSION, { ( "==" | ">" | "<" ), EXPRESSION } ;

EXPRESSION  = TERM, { ("+" | "-"), TERM } ;

TERM        = FACTOR, { ("*" | "/"), FACTOR } ;

FACTOR      = ( "+" | "-" | "!" ), FACTOR
            | IDENTIFIER
            | NUMBER
            | BOOL
            | NOTE
            | SEQUENCE
            | "(", EXPRESSION, ")" ;

NOTE_NAME   = LETTER, [ ACCIDENTAL ], DIGIT ;

TYPE        = "int" | "double" | "bool" | "note" | "sequence" | "string" ;

IDENTIFIER  = LETTER, { LETTER | DIGIT | "_" } ;

STRING      = '"', { CHARACTER }, '"' ;

CHARACTER   = LETTER | DIGIT | "_" ;

NUMBER      = DIGIT, { DIGIT }, [ ".", DIGIT, { DIGIT } ] ;

BOOL      = true | false ;

LETTER      = "A" | "B" | "C" | "D" | "E" | "F" | "G" ;

ACCIDENTAL  = "#" | "b" ;

DIGIT       = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;